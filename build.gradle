apply plugin: 'java'
apply plugin: 'application'

repositories {
    mavenCentral()

    flatDir {
        dirs './libs'
        // https://github.com/DiegoCatalano/Catalano-Framework/releases
        // Download and unpack from libs.zip:
        //  ./libs/Catalano.Core.jar
        //  ./libs/Catalano.Math.jar
        //  ./libs/Catalano.IO.jar
        //  ./libs/Catalano.Image.jar
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
}

mainClassName = "ksn.catalano.examples.filter.FilterUsageExample"
version = '0.0.2'

jar {
    manifest {
        attributes 'Main-Class' : mainClassName,
                   'Title'      : 'CatalanoExamples - demo Java SWING desktop application',
                   'Description': 'Examples of usage Catalano-Framework: https://github.com/DiegoCatalano/Catalano-Framework',
                   'Version'    : version,
                   'Vendor'     : 'SeregaLBN',
                   'Build-Date' : new Date().format("yyyy-MM-dd'T 'HH:mm:ss.SSSZZ")
    }
}

dependencies {
    compile 'org.slf4j:slf4j-simple:1.7.26'

    compile name: 'Catalano.Core'
    compile name: 'Catalano.Math'
    compile name: 'Catalano.IO'
    compile name: 'Catalano.Image'
}

// can run as single JAR:
//  java -jar ./CatalanoExamples/build/libs/CatalanoExamples-fat-{version}.jar
task fatJar(type: Jar) {
    manifest {
        attributes jar.manifest.attributes
    }
    baseName = project.name + '-fat'
    from {
        configurations.compile.collect {
            it.isDirectory()
                ? it
                : zipTree(it).matching{exclude{
                    //println it
                    //it.name == 'META-INF'
                    it.toString().contains('!META-INF')
                }}
        }
    }
    with jar
}
project.tasks.build.dependsOn project.tasks.fatJar
